plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

android {
    ndkVersion "21.3.6528147"
    compileSdk 30

    defaultConfig {
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug{ }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}


afterEvaluate {
    publishing {
        repositories {
            maven {
                url "https://gitlab.com/api/v4/projects/32431733/packages/maven"
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = 'Private-Token'
                    value = System.getenv("GITLAB_PRIVATE_TOKEN")
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "com.beeper"
                artifactId = "android-seshat"
                version = '0.1.1'
                pom {
                    name.set("Android Seshat")
                    description.set("Seshat bindings for Android")
                    url.set("https://beeper.com")
                    licenses {
                        license {
                            name.set("The Apache License, Version 2.0")
                            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://gitlab.com/beeper/android-seshat.git")
                        developerConnection.set("scm:git:ssh://gitlab.com/beeper/android-seshat.git")
                        url.set("https://gitlab.com/beeper/android-seshat.git")
                    }
                }
            }
        }
    }
}



dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2'
}

cargo {
    prebuiltToolchains = true
    verbose = true
    module  = "../bindings"
    libname = "android_seshat_bindings"
    targets = ["arm64","arm", "x86"]
    profile = 'release'
}

task runCargoBuild(type: Exec) {
    commandLine '../gradlew' args 'cargoBuild'
}

preBuild.dependsOn runCargoBuild